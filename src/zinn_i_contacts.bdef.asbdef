managed implementation in class ZBP_INN_I_CONTACTS unique;
strict ( 2 );
with draft;

extensible
{ with determinations on modify;
  with determinations on save;
  with validations on save; }

define behavior for ZINN_I_CONTACTS alias Contacts extensible
with additional save
persistent table zinn_contacts
draft table ZINN_CONTDRAFT
etag master LocinstLastchangeTstmpl //LocinstLastchangeDate
lock master total etag LastchangeTstmpl //LastchangeDate
authorization master( global )
early numbering

{
  field ( mandatory : create )
   ContactID;

  field ( readonly )
   LastchangeDate,
   LocinstLastchangeDate;

  field ( readonly : update )
   ContactID;


  create;
  update;
  delete;

  draft action Edit;
  draft action Activate optimized;
  draft action Discard;
  draft action Resume;
  draft determine action Prepare extensible { validation ValidateContactname; }
  static factory action Createdefault[1];
  event DefaultCreated parameter zinn_i_comments;

  validation ValidateContactname on save { create;update;  field Contactname; }

  determination defaultaddress on modify { field contactphone; }

  side effects
  {
    field contactphone affects field contactaddress;
  }

  mapping for ZINN_CONTACTS
  {
    ContactID = contactid;
    Contactname = contactname;
    Contactphone = contactphone;
    Contactaddress = contactaddress;
    LocinstLastchangeDate = locinst_lastchange_date;
    LocinstLastchangeTime = locinst_lastchange_time;
    LocinstLastchangeTstmpl = locinst_lastchange_tstmpl;
    LocinstLastchangeUtcl = locinst_lastchange_utcl;
    LastchangeDate = lastchange_date;
    LastchangeTime = lastchange_time;
    LastchangeTstmpl = lastchange_tstmpl;
    LastchangeUtcl = lastchange_utcl;
  }
}